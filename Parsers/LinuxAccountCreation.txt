// Account Creation Event Normalization Parser for Linux hosts
// This parser works against Linux hosts that have been onboarded through OMS/AMA agents. Events are written to "Syslog" table
// Usage Instruction : 
// Paste below query in log analytic, click on Save button and select as Function from drop down by specifying function name and alias as IPtables.
// Function usually takes 10-15 minutes to activate. You can then use function alias from any other queries (e.g. LinuxAccountCreation | take 10).
// Reference : Using functions in Azure monitor log queries : https://docs.microsoft.com/azure/azure-monitor/log-query/functions
//
//
let LinuxAccountCreation = Syslog
    | where SourceSystem == "Linux"
    | where ProcessName == "useradd"
    | where SyslogMessage !contains "failed"
    | where SyslogMessage has_cs "user"
    | extend EventVendor = 'Linux'
    | extend EventProduct = 'Linux'
    | parse SyslogMessage with *"name="username", UID="uid", "*
    | extend DestinationUserName = username
    | extend DestinationUserID = uid
    | extend UserGroupId = extract("GID=(\\d+),", 1, SyslogMessage)
    | extend DestinationUserHomePath = extract("home=(\\S+),", 1, SyslogMessage)
    | extend DestinationUserShell = extract("shell=(.*),", 1, SyslogMessage)
    | project
        TimeGenerated,
        DestinationUserName,
        DestinationUserHomePath,
        DestinationUserID,
        UserGroupId,
        HostName,
        HostIP,
        SeverityLevel,
        EventVendor,
        EventProduct;
LinuxAccountCreation
