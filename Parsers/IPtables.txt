// IPtables Event Parser for Linux hosts
// This parser works against Linux hosts that have been onboarded through OMS/AMA agents. Events are written to "Syslog" table
// Usage Instruction : 
// Paste below query in log analytics, click on Save button and select as Function from drop down by specifying function name and alias as IPtables.
// Function usually takes 10-15 minutes to activate. You can then use function alias from any other queries (e.g. IPtables | take 10).
// Reference : Using functions in Azure monitor log queries : https://docs.microsoft.com/azure/azure-monitor/log-query/functions
//
let iptablesEvents = Syslog
    | order by TimeGenerated
    | where ProcessName == "kernel"
    | where SeverityLevel == "warning"
    | where SyslogMessage !contains "kauditd"
    | where SyslogMessage !contains "audit_printk"
    | extend SourceIP = tostring(extract_all(@"((?:[0-9]{1,3}\.){3}[0-9]{1,3})", SyslogMessage)[0])
    | extend DestinationIP = tostring(extract_all(@"((?:[0-9]{1,3}\.){3}[0-9]{1,3})", SyslogMessage)[1])
    | extend DeviceMacAddress = tostring(extract_all(@'MAC=(\S+)', SyslogMessage)[0])
    | extend DestinationPort = tostring(extract_all(@'DPT=(\S+)', SyslogMessage)[0])
    | extend SourcePort = tostring(extract_all(@'SPT=(\S+)', SyslogMessage)[0])
    | extend TransportProtocol = tostring(extract_all(@'PROTO=(\S+)', SyslogMessage)[0])
    | extend DeviceInboundInterface = tostring(extract_all(@'IN=(\S+)', SyslogMessage)[0])
    | extend DeviceOutboundInterface = tostring(extract_all(@'OUT=(\S+)', SyslogMessage)[0])
    | extend ConnectionId = tostring(extract_all(@'ID=(\S+)', SyslogMessage)[0])
    | extend TTL = tostring(extract_all(@'TTL=(\S+)', SyslogMessage)[0])
    | extend EventName = tostring(extract_all(@'] (.*?):', SyslogMessage)[0])
    | extend DeviceAction = iff(EventName contains "DROP", "Drop", "Connect")
    | extend EventVendor = 'IPTABLES'
    | extend EventProduct = 'IPTABLES'
;
iptablesEvents
| project
    TimeGenerated,
    EventName,
    SourceIP,
    HostName,
    DestinationIP,
    SourcePort,
    DestinationPort,
    TransportProtocol,
    DeviceAction,
    DeviceInboundInterface,
    DeviceOutboundInterface,
    TTL,
    SyslogMessage